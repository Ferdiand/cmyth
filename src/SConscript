#
# cmyth apps
#

Import('env')

prefix = env['PREFIX']

libs = [ 'cmyth', 'pthread', 'refmem', ]
targets = [ ]

if env['HAS_MYSQL'] == 'yes':
    libs += [ 'mysqlclient' ]

conf = Configure(env)
if conf.CheckLib('fuse'):
    mythfuse = env.Program('mythfuse', 'mythfuse.c',
                           CCFLAGS = ' -D_FILE_OFFSET_BITS=64',
                           LINKFLAGS = env['LDFLAGS'],
                           CPPPATH = [ '../include' ],
                           LIBS = libs + [ 'fuse' ],
                           LIBPATH = [ '../libcmyth', '../librefmem' ])
    targets += [ mythfuse ]
env = conf.Finish()

mythping = env.Program('mythping', 'mythping.c',
                       LINKFLAGS = env['LDFLAGS'],
                       CPPPATH = [ '../include' ],
                       LIBS = libs,
                       LIBPATH = [ '../libcmyth', '../librefmem' ])

mythcat = env.Program('mythcat', 'mythcat.c',
                      LINKFLAGS = env['LDFLAGS'],
                      CPPPATH = [ '../include' ],
                      LIBS = libs,
                      LIBPATH = [ '../libcmyth', '../librefmem' ])

targets += [ mythping, mythcat ]

env.Install(prefix + '/bin', targets)

Return('targets')
